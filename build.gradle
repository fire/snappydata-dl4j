plugins {
  id 'java'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id 'com.commercehub.gradle.plugin.avro' version "0.5.0"
}

archivesBaseName = 'snappydata-dl4j'

shadowJar {
  zip64 true
}

allprojects {

  version = '0.7.0'

  repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/groups/public" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "http://repository.snappydata.io:8089/repository/internal" }
    maven { url "http://repository.snappydata.io:8089/repository/snapshots" }
    maven { url "http://mvnrepository.com/artifact" }
    maven { url 'https://clojars.org/repo' }
  }

  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'scala'
  apply plugin: 'idea'
  apply plugin: 'eclipse'
  apply plugin: "com.commercehub.gradle.plugin.avro"

  configurations {
    provided {
      description 'a dependency that is provided externally at runtime'
      visible true
    }
  }

  idea {
    module {
      scopes.PROVIDED.plus += [ configurations.provided ]
    }
  }
  eclipse {
    classpath {
      plusConfigurations += [ configurations.provided ]
    }
  }

  sourceSets {
    main.compileClasspath += configurations.provided
    main.runtimeClasspath -= configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
  }

  javadoc.classpath += configurations.provided

  ext {
    sparkVersion = '2.0.0'
    snappyVersion = '0.8'
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 4, 'hours'
  }

  dependencies {
    compile 'org.scala-lang:scala-library:2.11.6'
    compile 'org.scala-lang:scala-reflect:2.11.6'
    compile 'org.scala-lang:scala-compiler:2.11.6'
  }
}

dependencies {
  provided "io.snappydata:snappydata-core_2.11:${snappyVersion}"
  provided "io.snappydata:snappydata-cluster_2.11:${snappyVersion}"
  provided "io.snappydata:snappy-spark-core_2.11:${sparkVersion}"
  provided "io.snappydata:snappy-spark-catalyst_2.11:${sparkVersion}"
  provided "io.snappydata:snappy-spark-sql_2.11:${sparkVersion}"
  provided "io.snappydata:snappydata-aqp_2.11:${snappyVersion}"

  // https://mvnrepository.com/artifact/org.deeplearning4j/deeplearning4j-core
  compile group: 'org.deeplearning4j', name: 'deeplearning4j-core', version: '0.8.0'
  // https://mvnrepository.com/artifact/com.beust/jcommander
  compile group: 'com.beust', name: 'jcommander', version: '1.69'
  // https://mvnrepository.com/artifact/org.nd4j/nd4j-native-platform
  compile group: 'org.nd4j', name: 'nd4j-native-platform', version: '0.8.0'
  // https://mvnrepository.com/artifact/org.nd4j/nd4j-native
  compile group: 'org.nd4j', name: 'nd4j-native', version: '0.8.0'
  // https://mvnrepository.com/artifact/org.nd4j/nd4j-cuda-7.5
  compile group: 'org.nd4j', name: 'nd4j-cuda-7.5', version: '0.8.0'
}
